// inutil grammar and lex defns

// LEXER

// regular defns
_uppercase: 'A'-'Z' ;
_lowercase: 'a'-'z' ;
_letter: _uppercase | _lowercase | '_' ;
_digit: '0'-'9' ;
_lineComment: '/' '/' {.} '\n' ;
_blockComment: '/' '*' {. | '*'} '*' '/' ;
_posFloatLit: [('0' | '1'-'9' {_digit})] ('.' _digit {_digit});

// ignore whitespaces and comments
!whitespace: '\t' | '\n' | '\r' | ' ' ;
!comment : _lineComment | _blockComment ;

// keywords
var: 'v' 'a' 'r';
func: 'f' 'u' 'n' 'c' ;
struct: 's' 't' 'r' 'u' 'c' 't' ;
type: 't' 'y' 'p' 'e' ;
return: 'r' 'e' 't' 'u' 'r' 'n' ;
if: 'i' 'f' ;
else: 'e' 'l' 's' 'e' ;
for: 'f' 'o' 'r' ;

identifier: _letter {_letter | _digit} ;

// literals
integerLit: 
        ['+' | '-'] '1'-'9' {_digit}
    |   ['+' | '-'] '0'
    ;

floatLit: 
        ['+' | '-'] _posFloatLit
    ;

<<
    import(
        _ "inutil/token"
        "inutil/util"
        _ "inutil/runtime/table"
        _ "inutil/runtime/buffer"
    )
>>

// PARSER

Program 
    :   Functions
    ;

Functions
    :   Functions Function
    |   "empty"
    ;

Function
    :   func identifier "(" Args ")" StatementBlock
    ;

StatementBlock
    :   "{" Statements "}"
    ;

Args 
    :   Args "," identifier
    |   identifier
    |   "empty"
    ;

Statements 
    :   Statements Statement
    |   StatementBlock
    |   "empty"
    ;

 Statement
    :   ControlStatements
    |   AssignStatement ";"
    |   FunctionCall ";"
    ;

AssignStatement
    :   Lhs "=" Rhs
    ;

Lhs
    :   identifier
    ;

Rhs
    :   Expression
    ;

ControlStatements
    :   IfStatement ElseStatement
    |   ForStatement
    ;

IfStatement
    :   if Expression StatementBlock
    ;

ElseStatement
    :   else StatementBlock
    |   "empty"
    ;

// @TODO: fix for exprs ? kinda fixed !!
ForStatement
    :   for ForExpression ";" ForExpression ";" ForExpression StatementBlock
    ;

ForExpression
    :   Rhs
    |   AssignStatement
    |   "empty"
    ;

FunctionCall
    :   identifier "(" Params ")"
    ;

Params
    :   Params "," Param
    |   Param
    |   "empty"
    ;

Param
    :   Rhs
    ;

BoolOp
    :   "&&"
    |   "||"
    ;
    
RelOp
    :   "<="
    |   ">="
    |   "!="
    |   "=="
    |   "<"
    |   ">"
    ;

ArithOp1
    :   "/"
    |   "*"
    ;

ArithOp2
    :   "+"
    |   "-"
    ;

Expression
    :   Expression ArithOp2 Term
    |   Expression RelOp Term
    |   Expression BoolOp Term
    |   Term
    ;

Term
    :   Term ArithOp1 Factor
    |   Factor
    ;

Factor
    :   "(" Expression ")"  << $1, nil >>
    |   "-" Factor          << -$1 >>
    |   identifier
    |   integerLit          << util.IntValue( $0.(*token.Token).Lit ) >>
    |   floatLit            << util.FloatValue( $0.(*token.Token).Lit ) >>
    |   FunctionCall
    ;